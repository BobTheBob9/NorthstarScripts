untyped
//global const float COOP_RESPAWN_DELAY = 5.0 // this crashes for some reason???

global function CoopSpUtils_Init

global function GetPlayerToSpawnOn
global function RestartWithoutDroppingPlayers

global function IsPlayingTimeshiftLevel
global function GetPlayersInTimeline

void function CoopSpUtils_Init()
{
	AddCallback_OnClientConnecting( OnClientConnecting )
}

void function OnClientConnecting( entity player )
{
	// add custom script vars
	if ( IsPlayingTimeshiftLevel() )
	{
		player.s.timeline <- 1 // TIMEZONE_NIGHT
		player.s.isTimeTraveling <- false
		player.s.lastGoodTimeshiftPosOvergrown <- <0, 0, 0>
		player.s.lastGoodTimeshiftPosPristine <- <0, 0, 0>
	}
}

entity ornull function GetPlayerToSpawnOn() 
{
	array< entity > possiblePlayers
	foreach ( entity player in GetPlayerArray())
	{
		if ( IsAlive( player ))
			possiblePlayers.append( player )
	}
		
	if ( possiblePlayers.len() == 0 )
		return null // everyone is dead
		
	return possiblePlayers[ RandomInt( possiblePlayers.len() ) ]
}

void function RestartWithoutDroppingPlayers()
{
	// todo: make this deal with checkpoints/saves
	ServerCommand( "changelevel " + GetMapName() )
}


//EFFECT AND CAUSE/TIMESHIFT STUFF


bool function IsPlayingTimeshiftLevel()
{
	// sp_timeshift levels don't always start with level.allowTimeTravel == true, so we need to do the name check too
	var idx = GetMapName().find( "sp_timeshift" )
	return idx == 0 || level.allowTimeTravel == true
}

array< entity > function GetPlayersInTimeline( int timeline )
{
	array< entity > playersInTimeline
	foreach ( entity player in GetPlayerArray() )
	{
		if ( player.s.timeline == timeline )
			playersInTimeline.append( player )
	}
	
	return playersInTimeline
}